REM --------------------------------------------------------------------------
REM DND1-Named.bas Version 1.5 - Restructured by Dejay Clayton, 2014
REM Version history is available at the end of this document
REM Original source code (C)1997-2014 Richard Garriott
REM --------------------------------------------------------------------------

REM --------------------------------------------------------------------------
REM This version contains the following features:
REM
REM  * All variable names have been replaced with verbose counterparts
REM  * Most "magic numbers" have been replaced with verbose constants
REM  * Misspelled BASIC commands have been corrected
REM  * For consistency, all variations of the GOTO command are now 'GOTO'
REM  * Some line number errors have been fixed
REM --------------------------------------------------------------------------

REM DND-1
REM 1500 lines
REM (C)1977-2014 RICHARD GARRIOTT
REM 79/02/28. 19.27.34.
PROGRAM   DND1

00001 GOSUB 99000
00010 LET DIFFICULTY_LEVEL=1
00030 PRINT
00100 BASE 0
00110 LET PLAYER_EQUIPMENT_COUNT=0
00120 LET PLAYER_EQUIPPED_ITEM=EQUIPMENT_NONE
00130 LET CURRENT_MONSTER=MONSTER_NONE
00140 LET PLAYER_CLERIC_SPELL_COUNT=0
00150 LET SELECTED_CLERIC_SPELL_SLOT=0
00160 LET UNUSED_VAR_RANDOMIZE=RND(CLK(UNUSED_VAR_RANDOMIZE))
00170 DIM PLAYER_ATTRS(PLAYER_ATTRS_COUNT),PLAYER_ATTR_LABELS$(PLAYER_ATTRS_COUNT),PLAYER_EQUIPMENT_SLOTS(100),DUNGEON_MAP(50,50),EQUIPMENT_PRICES(100),EQUIPMENT_NAMES(100),MONSTERS_ATTRS(100,MONSTER_ATTRS_COUNT),MONSTER_NAMES$(100)
00180 DIM UNUSED_VAR_2(100),UNUSED_VAR_3(100),CLERIC_SPELL_COSTS(100),WIZARD_SPELL_COSTS(100),PLAYER_CLERIC_SPELL_SLOTS(100),PLAYER_WIZARD_SPELL_SLOTS(100)
00190 LET PLAYER_Y_COORD=INT(RND(0)*24+2)
00200 LET PLAYER_X_COORD=INT(RND(0)*24+2)
00210 FILE #1="DNG1"
00220 FILE #2="DNG2",#3="DNG3",#4="DNG4",#5="DNG5",#6="DNG6"
00230 RESTORE #4
00240 FILE #7="GMSTR"
00245 RESTORE #7
00250 RESTORE #1
00260 RESTORE #2
00261 RESTORE #3
00262 RESTORE #4
00263 RESTORE #5
00264 RESTORE #6
00270 DATA "STR","DEX","CON","CHAR","WIS","INT","GOLD"
00280 DATA "SWORD",10,"2-H-SWORD",15,"DAGGER",3,"MACE",5
00290 DATA "SPEAR",2,"BOW",25,"ARROWS",2,"LEATHER MAIL",15
00300 DATA "CHAIN MAIL",30,"TLTE MAIL",50,"ROPE",1,"SPIKES",1
00310 DATA "FLASK OF OIL",2,"SILVER CROSS",25,"SPARE FOOD",5
00320 PRINT "     DUNGEONS AND DRAGONS #1"
00330 PRINT
00340 PRINT "DO YOU NEED INSTUCTIONS ";
00350 INPUT USER_INPUT$
00360 IF USER_INPUT$="YES" THEN 01730
00370 IF USER_INPUT$="Y" THEN 00720
00380 PRINT "OLD OR NEW GAME";
00390 INPUT USER_INPUT$
00400 IF USER_INPUT$="OLD" THEN 01770
00410 PRINT "DUNGEON #";
00420 INPUT CURRENT_DUNGEON
00421 PRINT "CONTINUES RESET 1=YES,2=NO ";
00422 INPUT MONSTERS_LEVEL_UP_UPON_VICTORY
00430 REM ROLLING CHARICTERISTICS
00440 PRINT "PLAYERS NME ";
00450 INPUT PLAYER_NAME$
00460 IF PLAYER_NAME$<>"SHAVS" THEN 01730
00465 FOR LOOP=1 TO PLAYER_ATTRS_COUNT
00466 READ PLAYER_ATTR_LABELS$(LOOP)
00467 NEXT LOOP
00470 FOR LOOP=1 TO PLAYER_ATTRS_COUNT
00490 FOR INNER_LOOP=1 TO PLAYER_ATTRS_DICE_ROLLS
00500 LET PLAYER_ATTR_ROLL=INT(RND(0)*DICE_SIX_SIDED+1)
00510 LET PLAYER_ATTRS(LOOP)=PLAYER_ATTRS(LOOP)+PLAYER_ATTR_ROLL
00520 NEXT INNER_LOOP
00530 IF LOOP<>PLAYER_ATTR_GOLD THEN 00550
00540 LET PLAYER_ATTRS(LOOP)=PLAYER_ATTRS(LOOP)*15
00550 REM
00560 PRINT PLAYER_ATTR_LABELS$(LOOP);"=";PLAYER_ATTRS(LOOP)
00570 NEXT LOOP
00580 PRINT
00590 PRINT "CLASSIFICATION"
00600 PRINT "WHICH DO YOU WANT TO BE"
00610 PRINT "FIGHTER ,CLERIC ,OR WIZARD";
00620 INPUT PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)
00625 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)<>"NONE" THEN 00630
00626 FOR LOOP=0 TO PLAYER_ATTRS_COUNT
00627 LET PLAYER_ATTRS(LOOP)=0
00628 NEXT LOOP
00629 GOTO 00470
00630 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="FIGHTER" THEN 00770
00640 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="CLERIC" THEN 00810
00650 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="WIZARD" THEN 00790
00660 GOTO 00620
00670 PRINT "BUYING WEAPONS"
00680 PRINT "FAST OR NORM"
00690 INPUT BUY_MODE$
00700 PRINT "NUMBER","ITEM","PRICE"
00705 PRINT "-1-STOP"
00710 FOR LOOP=EQUIPMENT_FIRST TO EQUIPMENT_LAST
00720 READ EQUIPMENT_NAMES(LOOP),EQUIPMENT_PRICES(LOOP)
00725 IF BUY_MODE$="FAST" THEN 00740
00730 PRINT LOOP,EQUIPMENT_NAMES(LOOP),EQUIPMENT_PRICES(LOOP)
00740 NEXT LOOP
00750 GOSUB 01150
00760 GOTO 00830
00770 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=INT(RND(0)*8+1)
00780 GOTO 00670
00790 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=INT(RND(0)*4+1)
00800 GOTO 00670
00810 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=INT(RND(0)*6+1)
00820 GOTO 00670
00830 REM
00850 LET PLAYER_EQUIPMENT_COUNT=PLAYER_EQUIPMENT_COUNT+1
00860 INPUT SELECTED_WEAPON
00870 REM
00880 IF SELECTED_WEAPON<EQUIPMENT_NONE THEN 01000
00885 IF SELECTED_WEAPON>EQUIPMENT_SPARE_FOOD THEN 01000
00890 IF PLAYER_ATTRS(PLAYER_ATTR_GOLD)-EQUIPMENT_PRICES(SELECTED_WEAPON)<0 THEN 00970
00900 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="CLERIC" THEN 01290
00910 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="WIZARD" THEN 01350
00920 REM
00930 LET PLAYER_ATTRS(PLAYER_ATTR_GOLD)=PLAYER_ATTRS(PLAYER_ATTR_GOLD)-EQUIPMENT_PRICES(SELECTED_WEAPON)
00940 PRINT "GP= ";PLAYER_ATTRS(PLAYER_ATTR_GOLD)
00950 LET PLAYER_EQUIPMENT_SLOTS(PLAYER_EQUIPMENT_COUNT)=SELECTED_WEAPON
00960 GOTO 00830
00970 PRINT "COSTS TOO MUCH"
00980 PRINT "TRY AGAIN ";
00990 GOTO 00860
01000 PRINT "GP= ";PLAYER_ATTRS(PLAYER_ATTR_GOLD)
01010 REM
01020 PRINT "EQ LIST ";
01030 INPUT USER_INPUT$
01040 IF USER_INPUT$="NO" THEN 01090
01050 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
01060 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_NONE THEN 01080
01070 PRINT PLAYER_EQUIPMENT_SLOTS(LOOP),EQUIPMENT_NAMES(PLAYER_EQUIPMENT_SLOTS(LOOP))
01080 NEXT LOOP
01090 PRINT "YOUR CHARACTERISTICS ARE;"
01100 PRINT PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)
01101 IF PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)<>1 THEN 01110
01102 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=2
01110 PRINT "HIT POINTS",PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)
01120 PRINT
01130 PRINT
01140 GOTO 01400
01150 DATA "MAN",1,13,26,1,1,500
01160 DATA "GOBLIN",2,13,24,1,1,600
01170 DATA "TROLL",3,15,35,1,1,1000
01180 DATA "SKELETON",4,22,12,1,1,50
01190 DATA "BALROG",5,18,110,1,1,5000
01200 DATA "OCHRE JELLY",6,11,20,1,1,0
01210 DATA "GREY OOZE",7,11,13,1,1,0
01220 DATA "GNOME",8,13,30,1,1,100
01230 DATA "KOBOLD",9,15,16,1,1,500
01240 DATA "MUMMY",10,16,30,1,1,100
01250 FOR LOOP=MONSTER_FIRST TO MONSTER_LAST
01260 READ MONSTER_NAMES$(LOOP),MONSTERS_ATTRS(LOOP,MONSTER_ATTR_LEVEL),MONSTERS_ATTRS(LOOP,2),MONSTERS_ATTRS(LOOP,MONSTER_ATTR_HITPOINTS),MONSTERS_ATTRS(LOOP,MONSTER_ATTR_BASE_HITPOINTS),MONSTERS_ATTRS(LOOP,MONSTER_ATTR_BASE_GOLD),MONSTERS_ATTRS(LOOP,MONSTER_ATTR_GOLD)
01265 LET MONSTERS_ATTRS(LOOP,MONSTER_ATTR_BASE_HITPOINTS)=MONSTERS_ATTRS(LOOP,MONSTER_ATTR_HITPOINTS)
01267 LET MONSTERS_ATTRS(LOOP,MONSTER_ATTR_BASE_GOLD)=MONSTERS_ATTRS(LOOP,MONSTER_ATTR_GOLD)
01269 LET MONSTERS_ATTRS(LOOP,MONSTER_ATTR_LEVEL)=1
01270 NEXT LOOP
01280 RETURN
01290 IF SELECTED_WEAPON=EQUIPMENT_MACE THEN 00920
01300 IF SELECTED_WEAPON=EQUIPMENT_LEATHER_MAIL THEN 00920
01310 IF SELECTED_WEAPON=EQUIPMENT_CHAIN_MAIL THEN 00920
01320 IF SELECTED_WEAPON>EQUIPMENT_PLATE_MAIL THEN 00920
01330 PRINT "YOUR A CLERIC YOU CANT USE THAT "
01340 GOTO 00860
01350 IF SELECTED_WEAPON=EQUIPMENT_DAGGER THEN 00920
01360 IF SELECTED_WEAPON=EQUIPMENT_LEATHER_MAIL THEN 00920
01370 IF SELECTED_WEAPON>EQUIPMENT_PLATE_MAIL THEN 00920
01380 PRINT "YOUR A WIZARD YOU CANT USE THAT "
01390 GOTO 00860
01400 REM READ DUNGEON AND START GAME
01410 RESTORE #CURRENT_DUNGEON
01415 PRINT "READING DUNGEON NUM. ";CURRENT_DUNGEON
01420 FOR LOOP=0 TO 25
01430 FOR INNER_LOOP=0 TO 25
01431 LET DUNGEON_MAP(LOOP,INNER_LOOP)=MAP_ITEM_NONE
01432 IF CURRENT_DUNGEON=0 THEN 01450
01440 READ #CURRENT_DUNGEON,DUNGEON_MAP(LOOP,INNER_LOOP)
01443 IF DUNGEON_MAP(LOOP,INNER_LOOP)<>MAP_ITEM_NONE THEN 01450
01445 IF RND(0)<DICE_CRITICAL_HIT_THRESHOLD THEN 01447
01446 LET DUNGEON_MAP(LOOP,INNER_LOOP)=MAP_ITEM_STRENGTH_TRAP
01447 IF RND(0)<DICE_CRITICAL_HIT_THRESHOLD THEN 01450
01448 LET DUNGEON_MAP(LOOP,INNER_LOOP)=MAP_ITEM_CONSTITUTION_TRAP
01450 NEXT INNER_LOOP
01460 NEXT LOOP
01470 REM YEA START
01480 PRINT
01490 PRINT
01500 PRINT
01510 PRINT "WELCOME TO DUNGEON #";CURRENT_DUNGEON
01520 PRINT "YOU ARE AT (";PLAYER_Y_COORD;",";PLAYER_X_COORD;")" REM ERROR COORDINATES SHOULD BE REVERSED IF (X,Y) NOTATION IS DESIRED
01530 PRINT
01540 PRINT "COMANDS LIST",
01541 INPUT USER_INPUT$
01542 IF USER_INPUT$<>"YES" THEN 01590
01550 PRINT
01560 PRINT "1=MOVE  2=OPEN DOOR  3=SEARCH FOR TRAPS AND SECRET DOORS"
01570 PRINT "4=SWITCH WEAPON HN HAND  5=FIGHT"
01580 PRINT "6=LOOK AROUND  7=SAVE GAME  8=USE MAGIC  9=BUY MAGIC"
01585 PRINT "0=PASS  11=BUY H.P."
01590 PRINT "COMMAND=";
01600 INPUT USER_INPUT_COMMAND
01605 IF USER_INPUT_COMMAND=COMMAND_BUY_HITPOINTS THEN 10830
01606 IF USER_INPUT_COMMAND=COMMAND_EDIT_MAP THEN 11000
01610 IF USER_INPUT_COMMAND=COMMAND_MOVE THEN 02170
01620 IF USER_INPUT_COMMAND=COMMAND_OPEN_DOOR THEN 03130
01630 IF USER_INPUT_COMMAND=COMMAND_SEARCH THEN 03430
01640 IF USER_INPUT_COMMAND=COMMAND_SWITCH_WEAPON_HAND THEN 03640
01650 IF USER_INPUT_COMMAND=COMMAND_FIGHT THEN 03750
01660 IF USER_INPUT_COMMAND=COMMAND_LOOK_AROUND THEN 06390
01670 IF USER_INPUT_COMMAND=COMMAND_SAVE THEN 06610
01680 IF USER_INPUT_COMMAND=COMMAND_USE_MAGIC_SPELL THEN 08680
01690 IF USER_INPUT_COMMAND=COMMAND_BUY_MAGIC_SPELL THEN 09980
01700 IF USER_INPUT_COMMAND=COMMAND_VIEW_MAP THEN 10730
01705 IF USER_INPUT_COMMAND=COMMAND_PASS THEN 07000
01710 PRINT "COME ON ";
01720 GOTO 01590
01730 REM INSTRUCTIONS
01740 PRINT "WHO SAID YOU COULD PLAY"
01750 STOP
01760 GOTO 00380
01770 REM READ OUT OLD GAME
01775 RESTORE #7
01780 READ #7,CURRENT_DUNGEON
01790 READ #7,PLAYER_EQUIPMENT_COUNT
01800 READ #7,PLAYER_EQUIPPED_ITEM
01810 READ #7,PLAYER_Y_COORD
01820 READ #7,PLAYER_X_COORD
01830 READ #7,CURRENT_MONSTER
01840 FOR LOOP=0 TO 25
01850 FOR INNER_LOOP=0 TO 25
01860 READ #7,DUNGEON_MAP(LOOP,INNER_LOOP)
01870 NEXT INNER_LOOP
01880 NEXT LOOP
01890 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
01900 READ #7,PLAYER_EQUIPMENT_SLOTS(LOOP)
01910 NEXT LOOP
01920 FOR LOOP=MONSTER_FIRST TO MONSTER_LAST
01930 READ #7,MONSTER_NAMES$(LOOP)
01940 FOR INNER_LOOP=1 TO MONSTER_ATTRS_COUNT
01950 READ #7,MONSTERS_ATTRS(LOOP,INNER_LOOP)
01960 NEXT INNER_LOOP
01970 NEXT LOOP
01980 FOR LOOP=0 TO PLAYER_ATTRS_COUNT
01990 READ #7,PLAYER_ATTR_LABELS$(LOOP)
02000 READ #7,PLAYER_ATTRS(LOOP)
02010 NEXT LOOP
02020 READ #7,PLAYER_NAME$
02030 READ #7,MONSTER_Y_COORD
02040 READ #7,MONSTER_X_COORD
02050 FOR LOOP=EQUIPMENT_FIRST TO EQUIPMENT_LAST
02060 READ #7,EQUIPMENT_NAMES(LOOP)
02070 NEXT LOOP
02080 READ #7,PLAYER_WIZARD_SPELL_COUNT
02090 FOR LOOP=1 TO PLAYER_WIZARD_SPELL_COUNT
02100 READ #7,PLAYER_WIZARD_SPELL_SLOTS(LOOP)
02110 NEXT LOOP
02120 READ #7,PLAYER_CLERIC_SPELL_COUNT
02130 FOR LOOP=1 TO PLAYER_CLERIC_SPELL_COUNT
02140 READ #7,PLAYER_CLERIC_SPELL_SLOTS(LOOP)
02150 NEXT LOOP
02151 READ #7,MONSTER_X_COORD
02152 READ #7,MONSTER_Y_COORD
02160 GOTO 01510
02170 REM MOVE
02175 PRINT "YOU ARE AT ";PLAYER_Y_COORD;" , ";PLAYER_X_COORD REM ERROR COORDINATES SHOULD BE REVERSED IF (X,Y) NOTATION IS DESIRED
02180 PRINT "  DOWN  RIGHT  LEFT  OR  UP"
02190 INPUT USER_INPUT$
02200 IF USER_INPUT$="RIGHT" THEN 02260
02205 IF USER_INPUT$="R" THEN 02260
02210 IF USER_INPUT$="LEFT" THEN 02290
02215 IF USER_INPUT$="L" THEN 02290
02220 IF USER_INPUT$="UP" THEN 02320
02225 IF USER_INPUT$="U" THEN 02320
02230 IF USER_INPUT$="DOWN" THEN 02350
02235 IF USER_INPUT$="D" THEN 02350
02240 GOTO 02180

02250 REM
02260 LET PLAYER_ACTION_Y_COORD_DIR=0
02270 LET PLAYER_ACTION_X_COORD_DIR=1
02280 GOTO 02370
02290 LET PLAYER_ACTION_Y_COORD_DIR=0
02300 LET PLAYER_ACTION_X_COORD_DIR=-1
02310 GOTO 02370
02320 LET PLAYER_ACTION_Y_COORD_DIR=-1
02330 LET PLAYER_ACTION_X_COORD_DIR=0
02340 GOTO 02370
02350 LET PLAYER_ACTION_Y_COORD_DIR=1
02360 LET PLAYER_ACTION_X_COORD_DIR=0
02370 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_NONE THEN 02430
02380 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_WALL THEN 02480
02390 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_TRAP THEN 02550
02400 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_SECRET_DOOR THEN 02990
02401 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_STRENGTH_TRAP THEN 02424
02402 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_CONSTITUTION_TRAP THEN 02426
02410 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_MONSTER THEN 03060
02411 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_GOLD_TRAP THEN 02413
02412 GOTO 02480

02413 PRINT "AH......GOLD......."
02414 LET DISCOVERED_GOLD_PIECES=INT(RND(0)*500+10)
02415 PRINT DISCOVERED_GOLD_PIECES;"PIECES"
02416 LET PLAYER_ATTRS(PLAYER_ATTR_GOLD)=PLAYER_ATTRS(PLAYER_ATTR_GOLD)+DISCOVERED_GOLD_PIECES
02417 PRINT "GP= ";PLAYER_ATTRS(PLAYER_ATTR_GOLD)

02418 LET DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_NONE
02419 IF RND(0)>.2 THEN 02430
02420 PRINT "       POISON      "
02421 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)-INT(RND(0)*4+1)
02422 PRINT "HP= ";PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)
02423 GOTO 02430

02424 LET PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)=PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)+1
02425 GOTO 02418

02426 LET PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)=PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)+1
02429 GOTO 02418

02430 LET PLAYER_Y_COORD=PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR
02440 LET PLAYER_X_COORD=PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR
02450 PRINT "DONE"
02460 GOTO 07000

02470 REM
02480 PRINT "YOU RAN INTO A WALL"
02490 IF RND(0)*12+1>9 THEN 02520
02500 PRINT "BUT NO DAMAGE WAS INFLICTED"
02510 GOTO 07000
02520 PRINT "AND LOOSE 1 HIT POINT"
02530 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)-1
02540 GOTO 07000
02550 PRINT "OOOOPS A TRAP AND YOU FELL IN "
02560 IF RND(0)*3>2 THEN 02580
02570 GOTO 02600
02580 PRINT "AND HIT POINTS LOOSE 1"
02590 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)-1
02600 PRINT "I HOPE YOU HAVE SOME SPIKES AND PREFERABLY ROPE"
02610 PRINT "LET ME SEE"
02620 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
02630 IF PLAYER_EQUIPMENT_SLOTS(LOOP)<>EQUIPMENT_SPIKES THEN 02660
02640 LET PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_NONE
02650 GOTO 02680
02660 NEXT LOOP
02670 GOTO 02740
02680 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
02690 IF PLAYER_EQUIPMENT_SLOTS(LOOP)<>EQUIPMENT_ROPE THEN 02720
02700 LET PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_NONE
02710 GOTO 02760
02720 NEXT LOOP
02730 GOTO 02890
02740 PRINT "NO SPIKES AH THATS TOO BAD CAUSE YOUR DEAD "
02750 STOP
02760 PRINT "GOOD BOTH"
02770 PRINT "YOU MANAGE TO GET OUT EASY"
02775 GOTO 02870
02780 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
02790 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_SPIKES THEN 02820
02800 NEXT LOOP
02810 IF UNUSED_VAR_1>1 THEN 02830
02820 LET PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_NONE
02830 GOTO 02870
02840 LET PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_NONE
02850 LET PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_NONE
02860 GOTO 02820
02870 PRINT "YOUR STANDING NEXT TO THE EDGE THOUGH I'D MOVE"
02880 GOTO 02170
02890 PRINT "NO ROPE BUT AT LEAS SPIKES"
02900 IF INT(RND(0)*3)+1=2 THEN 02960 REM ERROR, PROBABLY GOTO 02920
02910 GOTO 02770
02920 PRINT "YOU FALL HALF WAY UP"
02930 IF INT(RND(0)*6)>PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)/3 THEN 02960
02940 PRINT "TRY AGAIN "
02950 GOTO 02900
02960 PRINT "OOPS H.P. LOOSE 1"
02970 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)-1
02980 GOTO 02940

02990 IF INT(RND(0)*6)+1>4 THEN 03010
03000 GOTO 02480
03010 PRINT "YOU JUST RAN INTO A SECRET DOOR"
03020 PRINT "AND OPENED IT"
03030 LET PLAYER_Y_COORD=PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR
03040 LET PLAYER_X_COORD=PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR
03050 GOTO 02450

03060 PRINT "YOU RAN INTO THE MONSTER "
03070 PRINT "HE SHOVES YOU BACK"
03080 PRINT
03090 IF INT(RND(0)*2)+1=2 THEN 03120
03100 PRINT "YOU LOOSE 6 HIT POINT "
03110 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)-6
03120 GOTO 07000

03130 PRINT "DOOR LEFT RIGHT UP OR DOWN"
03140 INPUT USER_INPUT$
03150 IF USER_INPUT$="LEFT" THEN 03200
03155 IF USER_INPUT$="L" THEN 03200
03160 IF USER_INPUT$="RIGHT" THEN 03230
03165 IF USER_INPUT$="R" THEN 03230
03170 IF USER_INPUT$="UP" THEN 03260
03175 IF USER_INPUT$="U" THEN 03260
03180 IF USER_INPUT$="DOWN" THEN 03290
03185 IF USER_INPUT$="D" THEN 03290
03190 GOTO 03130
03200 LET PLAYER_ACTION_Y_COORD_DIR=0
03210 LET PLAYER_ACTION_X_COORD_DIR=-1
03220 GOTO 03310
03230 LET PLAYER_ACTION_Y_COORD_DIR=0
03240 LET PLAYER_ACTION_X_COORD_DIR=1
03250 GOTO 03310
03260 LET PLAYER_ACTION_Y_COORD_DIR=-1
03270 LET PLAYER_ACTION_X_COORD_DIR=0
03280 GOTO 03310
03290 LET PLAYER_ACTION_Y_COORD_DIR=1
03300 LET PLAYER_ACTION_X_COORD_DIR=0
03310 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_SECRET_DOOR_TOO THEN 03350
03320 IF DUNGEON_MAP(PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_SECRET_DOOR THEN 03350
03330 PRINT "THERE IS NOT A DOOR THERE"
03340 GOTO 01590
03350 PRINT "PUSH"
03360 IF INT(RND(0)*20)+1<PLAYER_ATTRS(PLAYER_ATTR_STRENGTH) THEN 03390
03370 PRINT "DIDNT BUDGE"
03380 GOTO 07000
03390 PRINT "ITS OPEN"
03400 LET PLAYER_Y_COORD=PLAYER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR
03410 LET PLAYER_X_COORD=PLAYER_X_COORD+PLAYER_ACTION_X_COORD_DIR
03420 GOTO 02450
03430 PRINT "SEARCH.........SEARCH...........SEARCH..........."
03440 IF INT(RND(0)*40)<PLAYER_ATTRS(PLAYER_ATTR_WISDOM)+PLAYER_ATTRS(PLAYER_ATTR_INTELLIGENCE) THEN 03470
03450 PRINT "NO NOT THAT YOU CAN TELL"
03460 GOTO 07000
03470 FOR LOOP=-1 TO 1
03480 FOR INNER_LOOP=-1 TO 1
03490 IF DUNGEON_MAP(PLAYER_Y_COORD+LOOP,PLAYER_X_COORD+INNER_LOOP)=MAP_ITEM_TRAP THEN 03550
03500 IF DUNGEON_MAP(PLAYER_Y_COORD+LOOP,PLAYER_X_COORD+INNER_LOOP)=MAP_ITEM_SECRET_DOOR THEN 03590
03510 NEXT INNER_LOOP
03520 NEXT LOOP

03530 REM
03540 GOTO 03450
03550 PRINT "YES THERE IS A TRAP"
03560 PRINT "IT IS ";LOOP;"VERTICALY  ";INNER_LOOP;"HORAZONTALY FROM YOU"
03570 LET UNUSED_VAR_5=1
03580 GOTO 03500
03590 PRINT "YES A DOOR"
03600 PRINT "IT IS AT ";LOOP;"VERTICALY  ";INNER_LOOP;"HORAZANTALY"
03610 LET UNUSED_VAR_5=1
03620 GOTO 03510

03630 REM
03640 PRINT "WHICH WEAPON WILL YOU HOLD, NUM OF WEAPON "
03650 INPUT SELECTED_WEAPON
03660 IF SELECTED_WEAPON=EQUIPMENT_NONE THEN 03720
03670 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
03680 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=SELECTED_WEAPON THEN 03720
03690 NEXT LOOP
03700 PRINT "SORRY YOU DONT HAVE THAT ONE"
03710 GOTO 03640
03720 PRINT "O.K. YOU ARE NOW HOLDING A ";EQUIPMENT_NAMES(SELECTED_WEAPON)
03730 LET PLAYER_EQUIPPED_ITEM=SELECTED_WEAPON
03740 GOTO 07000

03750 REM FIGHTING BACK
03760 PRINT "YOUR WEAPON IS ";EQUIPMENT_NAMES(PLAYER_EQUIPPED_ITEM)
03770 IF CURRENT_MONSTER=MONSTER_NONE THEN 01590
03780 PRINT MONSTER_NAMES$(CURRENT_MONSTER)
03790 PRINT "HP=";MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)
03800 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_NONE THEN 04460
03810 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_SWORD THEN 04680
03820 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_TWO_HANDED_SWORD THEN 04860
03830 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_DAGGER THEN 05040
03840 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_MACE THEN 05270
03850 IF PLAYER_EQUIPPED_ITEM>EQUIPMENT_MACE  THEN 03870
03860 GOTO 03880
03870 IF PLAYER_EQUIPPED_ITEM<EQUIPMENT_SPARE_FOOD THEN 05450
03880 PRINT "FOOD ???.... WELL O.K."
03890 PRINT "IS IT TO HIT OR DISTRACT";
03900 INPUT USER_INPUT$
03910 IF USER_INPUT$="HIT" THEN 04330
03920 PRINT "THROW A-A=VE,B-BELOW,L-LEFT,OR R-RIGHT OF THE MONSTER";
03930 LET PLAYER_ACTION_MOVE_MONSTER=PLAYER_ACTION_LURE_MONSTER
03940 INPUT USER_INPUT$
03950 IF USER_INPUT$="B" THEN 04010
03960 IF USER_INPUT$="A" THEN 04040
03970 IF USER_INPUT$="L" THEN 04070
03980 LET PLAYER_ACTION_Y_COORD_DIR=0
03990 LET PLAYER_ACTION_X_COORD_DIR=1
04000 GOTO 04120 REM ERROR THIS SHOULD PROBABLY BE 04100
04010 LET PLAYER_ACTION_Y_COORD_DIR=-1
04020 LET PLAYER_ACTION_X_COORD_DIR=0
04030 GOTO 04120 REM ERROR THIS SHOULD PROBABLY BE 04100
04040 LET PLAYER_ACTION_Y_COORD_DIR=1
04050 LET PLAYER_ACTION_X_COORD_DIR=0
04060 GOTO 04120 REM ERROR THIS SHOULD PROBABLY BE 04100
04070 LET PLAYER_ACTION_Y_COORD_DIR=0
04080 LET PLAYER_ACTION_X_COORD_DIR=-1
04090 GOTO 04120 REM ERROR THIS SHOULD PROBABLY BE 04100
04100 IF PLAYER_ACTION_MOVE_MONSTER=WIZARD_SPELL_PUSH THEN 04120
04110 IF RND(0)>.5 THEN 04140
04120 IF DUNGEON_MAP(MONSTER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,MONSTER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_NONE THEN 04220
04130 IF DUNGEON_MAP(MONSTER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR,MONSTER_X_COORD+PLAYER_ACTION_X_COORD_DIR)=MAP_ITEM_TRAP THEN 04280
04140 PRINT "DIDN'T WORK"
04150 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
04160 IF PLAYER_ACTION_MOVE_MONSTER=WIZARD_SPELL_PUSH THEN 07000 REM WARNING THIS LINE DOESN'T NEED TO APPEAR WITHIN THE LOOP
04170 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_SPARE_FOOD THEN 04190
04180 NEXT LOOP
04190 LET PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_NONE
04200 LET PLAYER_EQUIPPED_ITEM=EQUIPMENT_NONE
04210 GOTO 07000
04220 PRINT "MONSTER MOVED BACK"
04230 LET DUNGEON_MAP(MONSTER_Y_COORD,MONSTER_X_COORD)=MAP_ITEM_NONE
04240 LET MONSTER_Y_COORD=MONSTER_Y_COORD+PLAYER_ACTION_Y_COORD_DIR
04250 LET MONSTER_X_COORD=MONSTER_X_COORD+PLAYER_ACTION_X_COORD_DIR
04260 LET DUNGEON_MAP(MONSTER_Y_COORD,MONSTER_X_COORD)=MAP_ITEM_MONSTER
04270 GOTO 04150
04280 PRINT "GOOD WORK THE MONSTER FELL INTO A TRAP AND IS DEAD"
04290 LET CURRENT_MONSTER_STATE=MONSTER_STATE_DEAD
04300 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_GOLD)=0
04310 GOTO 07000
04320 GOTO 04150
04330 IF INT(RND(0)*20)+1=20 THEN 04380
04340 IF INT(RND(0)*20)+1>MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_COMBAT_SKILL)-PLAYER_ATTRS(PLAYER_ATTR_DEXTERITY)/3 THEN 04410
04350 IF INT(RND(0)*20)+1>10-PLAYER_ATTRS(PLAYER_ATTR_DEXTERITY)/3 THEN 04440
04360 PRINT "TOTAL MISS"
04370 GOTO 04150
04380 PRINT "DIRECT HIT"
04390 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)/6) REM ERROR THIS STATEMENT SHOULD PROBABLY BE FOLLOWED BY GOTO 04150

04400 REM
04410 PRINT "HIT"
04420 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)/8)
04430 GOTO 04150
04440 PRINT "YOU HIT HIM BUT NOT GOOD ENOUGH"
04450 GOTO 04150

04460 REM FISTS
04470 PRINT "DO YOU REALIZE YOU ARE BARE HANDED"
04480 PRINT "DO YOU WANT TO MAKE ANOTHER CHOICE";
04490 INPUT USER_INPUT$
04500 IF USER_INPUT$="NO" THEN 04520
04510 GOTO 01590
04520 PRINT "O.K. PUNCH BITE SCRATCH HIT ........"
04530 FOR LOOP=-1 TO 1
04540 FOR INNER_LOOP=-1 TO 1
04550 IF DUNGEON_MAP(PLAYER_Y_COORD+LOOP,PLAYER_X_COORD+INNER_LOOP)=MAP_ITEM_MONSTER THEN 04610
04560 NEXT INNER_LOOP
04570 NEXT LOOP
04580 PRINT "NO GOOD ONE"
04590 GOTO 01590

04600 REM
04610 IF INT(RND(0)*20)+1>MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_COMBAT_SKILL) THEN 04640
04620 PRINT "TERRIBLE NO GOOD"
04630 GOTO 07000
04640 PRINT "GOOD A HIT"
04650 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)/6)
04660 GOTO 01590

04670 REM
04680 PRINT "SWING"
04690 GOSUB 08410
04700 IF MONSTER_DISTANCE_FROM_PLAYER<2 THEN 04730
04710 PRINT "HE IS OUT OF RANGE"
04720 GOTO 07000
04730 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_MISS THEN 04840
04740 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_WEAK_HIT THEN 04820
04750 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_HIT THEN 04790
04760 PRINT "CRITICAL HIT"
04770 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)/2)
04780 GOTO 01590
04790 PRINT "GOOD HIT"
04800 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)*4/5)
04810 GOTO 01590
04820 PRINT "NOT GOOD ENOUGH"
04830 GOTO 01590
04840 PRINT "MISSED TOTALY"
04850 GOTO 07000
04860 PRINT "SWHNG"
04870 GOSUB 08410
04880 IF MONSTER_DISTANCE_FROM_PLAYER<2.1 THEN 04910
04890 PRINT "HE IS OUT OF RANGE"
04900 GOTO 07000
04910 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_MISS THEN 05020
04920 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_WEAK_HIT THEN 05000
04930 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_HIT THEN 04970
04940 PRINT "CRITICAL HIT"
04950 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)
04960 GOTO 01590
04970 PRINT "HIT"
04980 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)*5/7)
04990 GOTO 01590
05000 PRINT "HIT BUT ` WELL ENOUGH"
05010 GOTO 01590
05020 PRINT "MISSED TOTALY"
05030 GOTO 07000
05040 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
05050 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_DAGGER THEN 05090
05060 NEXT LOOP
05070 PRINT "YOU DONT HAVE A DGGER"
05080 GOTO 07000
05090 GOSUB 08410
05100 IF MONSTER_DISTANCE_FROM_PLAYER>5 THEN 04710
05110 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_MISS THEN 05200
05120 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_WEAK_HIT THEN 05220
05130 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_HIT THEN 05240
05140 PRINT "CRITICAL HIT"
05150 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)*3/10)
05160 IF MONSTER_DISTANCE_FROM_PLAYER<2 THEN 05190
05170 LET PLAYER_EQUIPMENT_SLOTS(PLAYER_EQUIPPED_ITEM)=EQUIPMENT_NONE REM ERROR THIS BUG WILL ALWAYS REMOVE THE THIRD PLAYER EQUIPMENT SLOT. INSTEAD, RECORD VALUE OF 'LOOP' AT LINE 05050 AND USE THAT INSTEAD OF PLAYER_EQUIPPED_ITEM HERE.
05180 LET PLAYER_EQUIPPED_ITEM=EQUIPMENT_NONE
05190 GOTO 07000
05200 PRINT "MISSED TOTALY"
05210 GOTO 05160
05220 PRINT "HIT BUT NO DAMAGE"
05230 GOTO 05160
05240 PRINT "HIT"
05250 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)/4)
05260 GOTO 05160
05270 PRINT "SWING"
05280 GOSUB 08410
05290 IF UNSET_VAR_1<2 THEN 04720 REM ERROR, PROBABLY SHOULD BE R1 INSTEAD
05300 GOTO 04710
05310 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_MISS THEN 05420
05320 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_WEAK_HIT THEN 05400
05330 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_HIT THEN 05370
05340 PRINT "CRITICAL HIT"
05350 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)*4/9)
05360 GOTO 01590
05370 PRINT "HIT"
05380 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)*5/11)
05390 GOTO 01590
05400 PRINT "HIT BUT NO DAMAGE"
05410 GOTO 01590
05420 PRINT "MISS"
05430 GOTO 07000

05440 REM
05450 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
05460 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=PLAYER_EQUIPPED_ITEM THEN 05500
05470 NEXT LOOP
05480 PRINT "NO WEAPON FOUND"
05490 GOTO 01590
05500 GOSUB 08410
05510 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_SPEAR THEN 05760
05520 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_BOW THEN 05800
05530 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_ARROWS THEN 05840
05540 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_LEATHER_MAIL THEN 05880
05550 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_CHAIN_MAIL THEN 05920
05560 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_PLATE_MAIL THEN 05960
05570 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_ROPE THEN 06000
05580 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_SPIKES THEN 06040
05590 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_FLASK_OF_OIL THEN 06080
05600 PRINT "AS A CLUB OR SIGHT";
05610 INPUT USER_INPUT$
05620 IF USER_INPUT$="SIGHT" THEN 05650
05630 IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_SILVER_CROSS THEN 06120
05640 GOTO 05480
05650 IF MONSTER_DISTANCE_FROM_PLAYER<10 THEN 05680
05660 PRINT "FAILED"
05670 GOTO 07000
05680 PRINT "THE MONSTER IS HURT"
05690 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=1/6
05700 IF CURRENT_MONSTER=MONSTER_GOBLIN THEN 06200
05710 IF CURRENT_MONSTER=MONSTER_MUMMY THEN 06200
05720 IF CURRENT_MONSTER=MONSTER_SKELETON THEN 06200
05730 GOTO 06260
05740 IF INT(RND(0)*0)>0 THEN 06260
05750 GOTO 06200
05760 LET PLAYER_WEAPON_RANGE=10
05770 LET PLAYER_HIT_STRENGTH_MULTIPLIER=3/7
05780 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=5/11
05790 GOTO 06160
05800 LET PLAYER_WEAPON_RANGE=15
05810 LET PLAYER_HIT_STRENGTH_MULTIPLIER=3/7
05820 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=5/11
05821 FOR EQUIPMENT_ARROW_SLOT=1 TO 100
05822 IF PLAYER_EQUIPMENT_SLOTS(EQUIPMENT_ARROW_SLOT)=EQUIPMENT_ARROWS THEN 05825
05823 NEXT EQUIPMENT_ARROW_SLOT
05824 GOTO 06280
05825 LET PLAYER_EQUIPPED_ITEM=EQUIPMENT_ARROWS
05826 LET PLAYER_EQUIPMENT_SLOTS(EQUIPMENT_ARROW_SLOT)=EQUIPMENT_NONE
05830 GOTO 06160
05840 LET PLAYER_WEAPON_RANGE=1.5
05850 LET PLAYER_HIT_STRENGTH_MULTIPLIER=1/7
05860 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=1/5
05870 GOTO 06160
05880 LET PLAYER_WEAPON_RANGE=4
05890 LET PLAYER_HIT_STRENGTH_MULTIPLIER=1/10
05900 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=1/8
05910 GOTO 06160
05920 LET PLAYER_WEAPON_RANGE=4
05930 LET PLAYER_HIT_STRENGTH_MULTIPLIER=1/7
05940 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=1/6
05950 GOTO 06160
05960 LET PLAYER_WEAPON_RANGE=3
05970 LET PLAYER_HIT_STRENGTH_MULTIPLIER=1/8
05980 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=1/5
05990 GOTO 06160
06000 LET PLAYER_WEAPON_RANGE=5
06010 LET PLAYER_HIT_STRENGTH_MULTIPLIER=1/9
06020 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=1/6
06030 GOTO 06160
06040 LET PLAYER_WEAPON_RANGE=8
06050 LET PLAYER_HIT_STRENGTH_MULTIPLIER=1/9
06060 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=1/4
06070 GOTO 06160
06080 LET PLAYER_WEAPON_RANGE=6
06090 LET PLAYER_HIT_STRENGTH_MULTIPLIER=1/3
06100 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=2/3
06110 GOTO 06160
06120 LET PLAYER_WEAPON_RANGE=1.5
06130 LET PLAYER_HIT_STRENGTH_MULTIPLIER=1/3
06140 LET PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER=1/2
06150 GOTO 06160
06160 IF MONSTER_DISTANCE_FROM_PLAYER>PLAYER_WEAPON_RANGE THEN 04710
06170 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_MISS THEN 06280
06180 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_WEAK_HIT THEN 06260
06190 IF PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_HIT THEN 06230
06200 PRINT "CRITICAL HIT"
06210 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)*PLAYER_CRITICAL_HIT_STRENGTH_MULTIPLIER)
06220 GOTO 06300
06230 PRINT "HIT"
06240 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-INT(PLAYER_ATTRS(PLAYER_ATTR_STRENGTH)*PLAYER_HIT_STRENGTH_MULTIPLIER)
06250 GOTO 06300
06260 PRINT "HIT BUT NO DAMAGE"
06270 GOTO 06300
06280 PRINT "MISS"
06290 GOTO 06300
06300 IF PLAYER_EQUIPMENT_SLOTS(PLAYER_EQUIPPED_ITEM)=EQUIPMENT_SILVER_CROSS THEN 07000 REM ERROR IF PLAYER_EQUIPPED_ITEM=EQUIPMENT_SILVER_CROSS
06310 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
06320 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=PLAYER_EQUIPPED_ITEM THEN 06340
06330 NEXT LOOP
06340 LET PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_NONE
06350 IF PLAYER_EQUIPPED_ITEM<>EQUIPMENT_ARROWS THEN 06360
06355 GOTO 06370
06360 LET PLAYER_EQUIPPED_ITEM=EQUIPMENT_NONE
06370 IF PLAYER_ATTACK_EFFECTIVENESS>PLAYER_ATTACK_MISS THEN 01590
06380 GOTO 07000

06390 REM LOOKING
06400 FOR LOOP=-5 TO 5
06410 FOR INNER_LOOP=-5 TO 5
06420 IF LOOP+PLAYER_Y_COORD>25 THEN 06510
06430 IF LOOP+PLAYER_Y_COORD<0 THEN 06510
06440 IF PLAYER_X_COORD+INNER_LOOP>25 THEN 06510
06450 IF PLAYER_X_COORD+INNER_LOOP<0 THEN 06510
06460 IF LOOP<>0 THEN 06480
06470 IF INNER_LOOP=0 THEN 06590
06480 IF DUNGEON_MAP(LOOP+PLAYER_Y_COORD,INNER_LOOP+PLAYER_X_COORD)=MAP_ITEM_TRAP THEN 06550
06485 IF DUNGEON_MAP(LOOP+PLAYER_Y_COORD,INNER_LOOP+PLAYER_X_COORD)=MAP_ITEM_STRENGTH_TRAP OR DUNGEON_MAP(LOOP+PLAYER_Y_COORD,INNER_LOOP+PLAYER_X_COORD)=MAP_ITEM_CONSTITUTION_TRAP THEN 06550
06490 IF DUNGEON_MAP(LOOP+PLAYER_Y_COORD,INNER_LOOP+PLAYER_X_COORD)=MAP_ITEM_SECRET_DOOR THEN 06570
06500 PRINT DUNGEON_MAP(LOOP+PLAYER_Y_COORD,INNER_LOOP+PLAYER_X_COORD);
06510 NEXT INNER_LOOP
06520 PRINT
06530 NEXT LOOP
06540 GOTO 07000
06550 PRINT 0;
06560 GOTO 06510
06570 PRINT 1;
06580 GOTO 06510
06590 PRINT 9;
06600 GOTO 06510

06610 REM SAVE GAME
06615 RESTORE #7
06620 WRITE #7,CURRENT_DUNGEON
06630 WRITE #7,PLAYER_EQUIPMENT_COUNT
06640 WRITE #7,PLAYER_EQUIPPED_ITEM
06650 WRITE #7,PLAYER_Y_COORD
06660 WRITE #7,PLAYER_X_COORD
06670 WRITE #7,CURRENT_MONSTER
06680 FOR LOOP=0 TO 25
06690 FOR INNER_LOOP=0 TO 25
06700 WRITE #7,DUNGEON_MAP(LOOP,INNER_LOOP)
06710 NEXT INNER_LOOP
06720 NEXT LOOP
06730 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
06740 WRITE #7,PLAYER_EQUIPMENT_SLOTS(LOOP)
06750 NEXT LOOP
06760 FOR LOOP=MONSTER_FIRST TO MONSTER_LAST
06770 WRITE #7,MONSTER_NAMES$(LOOP)
06780 FOR INNER_LOOP=1 TO MONSTER_ATTR_COUNT
06790 WRITE #7,MONSTERS_ATTRS(LOOP,INNER_LOOP)
06800 NEXT INNER_LOOP
06810 NEXT LOOP
06820 FOR LOOP=0 TO PLAYER_ATTRS_COUNT
06830 WRITE #7,PLAYER_ATTR_LABELS$(LOOP)
06840 WRITE #7,PLAYER_ATTRS(LOOP)
06850 NEXT LOOP
06860 WRITE #7,PLAYER_NAME$
06870 WRITE #7,MONSTER_Y_COORD
06880 FOR LOOP=1 TO 15
06890 WRITE #7,EQUIPMENT_NAMES(LOOP)
06900 NEXT LOOP
06910 WRITE #7,PLAYER_WIZARD_SPELL_COUNT
06920 FOR LOOP=1 TO PLAYER_WIZARD_SPELL_COUNT
06930 WRITE #7,PLAYER_WIZARD_SPELL_SLOTS(LOOP)
06940 NEXT LOOP
06950 WRITE #7,PLAYER_CLERIC_SPELL_COUNT
06960 FOR LOOP=1 TO PLAYER_CLERIC_SPELL_COUNT
06970 WRITE #7,PLAYER_CLERIC_SPELL_SLOTS(LOOP)
06971 NEXT LOOP
06972 WRITE #7,MONSTER_X_COORD
06980 WRITE #7,MONSTER_Y_COORD
06985 GOTO 01590
06990 STOP
07000 IF CURRENT_MONSTER_STATE=MONSTER_STATE_DEAD THEN 08290
07010 IF PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)<2 THEN 08160
07020 IF CURRENT_MONSTER>MONSTER_NONE THEN 07160
07030 IF PLAYER_Y_COORD<>1 THEN 07110
07040 IF PLAYER_X_COORD<>12 THEN 07110
07050 PRINT "SO YOU HAVE RETURNED"
07060 IF PLAYER_ATTRS(PLAYER_ATTR_GOLD)<100 THEN 07110
07070 LET PLAYER_ATTRS(PLAYER_ATTR_GOLD)=PLAYER_ATTRS(PLAYER_ATTR_GOLD)-100
07080 PRINT "WANT TO BUY MORE EQUIPMENT"
07090 INPUT USER_INPUT$
07100 IF USER_INPUT$="YES" THEN 07130
07110 IF RND(0)*20>10 THEN 07830
07120 GOTO 01590
07130 PRINT "YOUR H.P. ARE RESTORED 2 POINTS"
07140 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)+2
07150 GOTO 00830
07160 GOSUB 08410
07170 IF MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)<1 THEN 08290
07180 IF MONSTER_DISTANCE_FROM_PLAYER<2.0 THEN 07600
07190 IF UNSET_VAR_1>10 THEN 01590 REM ERROR, PROBABLY SHOULD BE R1 INSTEAD

07200 REM HE IS COMMING
07210 IF ABS(MONSTER_Y_DISTANCE_FROM_PLAYER)>ABS(MONSTER_X_DISTANCE_FROM_PLAYER) THEN 07260
07220 LET MONSTER_ACTION_Y_COORD_DIR=0
07230 IF LOOP=1 THEN 07270
07240 LET MONSTER_ACTION_X_COORD_DIR=-(MONSTER_X_DISTANCE_FROM_PLAYER/ABS(MONSTER_X_DISTANCE_FROM_PLAYER))
07250 GOTO 07280
07260 LET MONSTER_ACTION_Y_COORD_DIR=-(MONSTER_Y_DISTANCE_FROM_PLAYER/ABS(MONSTER_Y_DISTANCE_FROM_PLAYER))
07270 LET MONSTER_ACTION_X_COORD_DIR=0
07280 FOR MAP_ITEM_LOOP=MAP_ITEM_FIRST TO MAP_ITEM_LAST
07290 IF MAP_ITEM_LOOP=MAP_ITEM_WALL OR MAP_ITEM_LOOP=MAP_ITEM_MONSTER THEN 07320
07300 IF MONSTER_Y_COORD+MONSTER_ACTION_Y_COORD_DIR<0 OR MONSTER_Y_COORD+MONSTER_ACTION_Y_COORD_DIR>25 OR MONSTER_X_COORD+MONSTER_ACTION_X_COORD_DIR<0 OR MONSTER_X_COORD+MONSTER_ACTION_X_COORD_DIR>25 THEN 07320
07310 IF DUNGEON_MAP(MONSTER_Y_COORD+MONSTER_ACTION_Y_COORD_DIR,MONSTER_X_COORD+MONSTER_ACTION_X_COORD_DIR)=MAP_ITEM_LOOP THEN 07340
07320 NEXT MAP_ITEM_LOOP
07330 GOTO 07510
07340 IF MAP_ITEM_LOOP=MAP_ITEM_NONE THEN 07430
07345 IF MAP_ITEM_LOOP=MAP_ITEM_GOLD_TRAP OR MAP_ITEM_LOOP=MAP_ITEM_STRENGTH_TRAP OR MAP_ITEM_LOOP=MAP_ITEM_CONSTITUTION_TRAP THEN 07430
07350 IF MAP_ITEM_LOOP=MAP_ITEM_TRAP THEN 07530
07360 IF MAP_ITEM_LOOP=MAP_ITEM_SECRET_DOOR OR MAP_ITEM_LOOP=MAP_ITEM_SECRET_DOOR_TOO THEN 07380
07370 GOTO 07510

07380 REM THROUGH THE DOOR
07390 IF DUNGEON_MAP(MONSTER_Y_COORD+2*MONSTER_ACTION_Y_COORD_DIR,MONSTER_X_COORD+2*MONSTER_ACTION_X_COORD_DIR)<>MAP_ITEM_NONE THEN 07510
07400 LET MONSTER_ACTION_Y_COORD_DIR=MONSTER_ACTION_Y_COORD_DIR*2
07410 LET MONSTER_ACTION_X_COORD_DIR=MONSTER_ACTION_X_COORD_DIR*2
07420 GOTO 07440

07430 REM CLOSER
07440 LET DUNGEON_MAP(MONSTER_Y_COORD,MONSTER_X_COORD)=MAP_ITEM_NONE 
07450 LET MONSTER_Y_COORD=MONSTER_Y_COORD+MONSTER_ACTION_Y_COORD_DIR
07460 LET MONSTER_X_COORD=MONSTER_X_COORD+MONSTER_ACTION_X_COORD_DIR
07470 LET DUNGEON_MAP(MONSTER_Y_COORD,MONSTER_X_COORD)=MAP_ITEM_MONSTER
07480 GOSUB 08410

07490 REM
07500 GOTO 01590

07510 REM NOWHERE
07520 GOTO 07490
07530 PRINT "GOOD WORK  YOU LED HIM INTO A TRAP"
07540 LET CURRENT_MONSTER_STATE=MONSTER_STATE_DEAD
07550 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_GOLD)=0
07560 GOTO 07000
07570 LET MONSTER_Y_DISTANCE_FROM_PLAYER=-.5*MONSTER_Y_DISTANCE_FROM_PLAYER
07580 LET MONSTER_X_DISTANCE_FROM_PLAYER=-.5*MONSTER_X_DISTANCE_FROM_PLAYER
07590 GOTO 07420
07600 PRINT MONSTER_NAMES$(CURRENT_MONSTER);"WATCH IT"
07610 FOR LOOP=1 TO PLAYER_EQUIPMENT_COUNT
07620 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_PLATE_MAIL THEN 07720
07630 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_CHAIN_MAIL THEN 07700
07640 IF PLAYER_EQUIPMENT_SLOTS(LOOP)=EQUIPMENT_LEATHER_MAIL THEN 07680
07650 NEXT LOOP
07651 LET ARMOR_CLASS=6+PLAYER_ATTRS(PLAYER_ATTR_DEXTERITY)
07652 GOTO 07730
07660 LET ARMOR_CLASS=8+PLAYER_ATTRS(PLAYER_ATTR_DEXTERITY)
07670 GOTO 07730
07680 LET ARMOR_CLASS=12+PLAYER_ATTRS(PLAYER_ATTR_DEXTERITY)
07690 GOTO 07730
07700 LET ARMOR_CLASS=16+PLAYER_ATTRS(PLAYER_ATTR_DEXTERITY)
07710 GOTO 07730
07720 LET ARMOR_CLASS=20+PLAYER_ATTRS(PLAYER_ATTR_DEXTERITY)
07730 IF RND(0)*40>ARMOR_CLASS THEN 07790
07740 IF RND(0)*2>1 THEN 07770
07750 PRINT "HE MISSED"
07760 GOTO 01590
07770 PRINT "HE HIT YOU BUT NOT GOOD ENOUGH"
07780 GOTO 07000
07790 PRINT "MONSTER SCORES A HIT"
07800 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)-INT(RND(0)*MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_COMBAT_SKILL)+1)
07810 PRINT "H.P.=";PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)
07820 GOTO 07000
07830 FOR MONSTER_SPAWN_LOOP=1 TO 50
07840 FOR LOOP=1 TO 10
07850 IF MONSTERS_ATTRS(LOOP,MONSTER_ATTR_BASE_GOLD)>=1 AND RND(0)>.925 THEN 08000
07860 NEXT LOOP
07870 NEXT MONSTER_SPAWN_LOOP
07880 PRINT "ALL MONSTERS DEAD"
07890 PRINT "RESET";
07900 INPUT USER_INPUT$
07910 IF USER_INPUT$="YES" THEN 07930
07920 STOP

07930 REM
07931 LET DIFFICULTY_LEVEL=DIFFICULTY_LEVEL+1
07932 FOR LOOP=MONSTER_FIRST TO MONSTER_LAST
07950 LET MONSTERS_ATTRS(LOOP,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(LOOP,MONSTER_ATTR_BASE_HITPOINTS)*DIFFICULTY_LEVEL
07960 LET MONSTERS_ATTRS(LOOP,MONSTER_ATTR_GOLD)=MONSTERS_ATTRS(LOOP,MONSTER_ATTR_BASE_GOLD)*DIFFICULTY_LEVEL
07970 NEXT LOOP
07980 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)+5
07990 GOTO 01590
08000 LET CURRENT_MONSTER=LOOP
08010 LET MONSTER_SPAWN_DISTANCE_FROM_PLAYER=INT(RND(0)*7+1)
08015 FOR LOOP=-MONSTER_SPAWN_DISTANCE_FROM_PLAYER TO MONSTER_SPAWN_DISTANCE_FROM_PLAYER
08020 FOR INNER_LOOP=-MONSTER_SPAWN_DISTANCE_FROM_PLAYER TO MONSTER_SPAWN_DISTANCE_FROM_PLAYER
08025 IF ABS(LOOP)<=2 OR ABS(INNER_LOOP)<=2 THEN 08080
08030 IF PLAYER_Y_COORD+LOOP<1 THEN 08080
08040 IF PLAYER_X_COORD+INNER_LOOP<1 THEN 08080
08050 IF PLAYER_Y_COORD+LOOP>25 THEN 08080
08060 IF PLAYER_X_COORD+INNER_LOOP>25 THEN 08080
08065 IF RND(0)>.7 THEN 08080
08070 IF DUNGEON_MAP(PLAYER_Y_COORD+LOOP,PLAYER_X_COORD+INNER_LOOP)=MAP_ITEM_NONE THEN 08110
08080 NEXT INNER_LOOP
08090 NEXT LOOP
08100 GOTO 08010

08110 REM
08120 LET DUNGEON_MAP(PLAYER_Y_COORD+LOOP,PLAYER_X_COORD+INNER_LOOP)=MAP_ITEM_MONSTER
08130 LET MONSTER_Y_COORD=PLAYER_Y_COORD+LOOP
08140 LET MONSTER_X_COORD=PLAYER_X_COORD+INNER_LOOP
08150 GOTO 07000
08160 IF PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)<1 THEN 08190
08170 PRINT "WATCH IT H.P.=";PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)
08180 GOTO 07020
08190 IF PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)<0 THEN 08250
08200 IF PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)<9 THEN 08230
08210 PRINT "H.P.=0 BUT CONST. HOLDS"
08220 GOTO 07020
08230 PRINT "SORRY YOUR DEAD"
08240 STOP
08250 IF PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)<9 THEN 08230
08260 LET PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)=PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)-2
08270 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)+1
08280 GOTO 08190
08290 LET CURRENT_MONSTER_STATE=MONSTER_STATE_ALIVE
08300 LET PLAYER_ATTRS(PLAYER_ATTR_GOLD)=PLAYER_ATTRS(PLAYER_ATTR_GOLD)+MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_GOLD)
08310 LET DUNGEON_MAP(MONSTER_Y_COORD,MONSTER_X_COORD)=MAP_ITEM_NONE
08320 LET MONSTER_Y_COORD=0
08330 LET MONSTER_X_COORD=0
08340 PRINT "GOOD WORK YOU JUST KILLED A ";MONSTER_NAMES$(CURRENT_MONSTER)
08350 PRINT "AND GET ";MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_GOLD);"GOLD PIECES"
08355 IF MONSTERS_LEVEL_UP_UPON_VICTORY=1 GOTO 08370
08360 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_BASE_GOLD)=0
08370 PRINT "YOU HAVE";PLAYER_ATTRS(PLAYER_ATTR_GOLD);" GOLD "
08380 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_GOLD)=0
08381 IF MONSTERS_LEVEL_UP_UPON_VICTORY<>1 GOTO 08390
08382 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_BASE_HITPOINTS)*MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_LEVEL)/1
08383 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_GOLD)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_BASE_GOLD)*MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_LEVEL)
08390 LET CURRENT_MONSTER=MONSTER_NONE
08400 GOTO 07000

08410 REM RANGE AND HIT CHECK
08420 FOR LOOP=-25 TO 25
08430 FOR INNER_LOOP=-25 TO 25
08440 IF PLAYER_Y_COORD+LOOP>25 THEN 08490
08450 IF PLAYER_Y_COORD+LOOP<0 THEN 08490
08460 IF PLAYER_X_COORD+INNER_LOOP>25 THEN 08490
08470 IF PLAYER_X_COORD+INNER_LOOP<0 THEN 08490
08480 IF DUNGEON_MAP(PLAYER_Y_COORD+LOOP,PLAYER_X_COORD+INNER_LOOP)=MAP_ITEM_MONSTER THEN 08520
08490 NEXT INNER_LOOP
08500 NEXT LOOP
08510 LET MONSTER_DISTANCE_FROM_PLAYER=1000
08520 LET MONSTER_Y_DISTANCE_FROM_PLAYER=LOOP
08530 LET MONSTER_X_DISTANCE_FROM_PLAYER=INNER_LOOP
08540 IF MONSTER_DISTANCE_FROM_PLAYER=1000 THEN 08570
08550 LET MONSTER_DISTANCE_FROM_PLAYER=SQR(LOOP*LOOP+INNER_LOOP*INNER_LOOP)
08570 IF INT(RND(0)*20 +1)>18 THEN 08620
08580 IF RND(0)*20>MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_COMBAT_SKILL)-PLAYER_ATTRS(PLAYER_ATTR_DEXTERITY)/3 THEN 08640
08590 IF RND(0)*2>1.7 THEN 08660
08600 LET PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_MISS
08610 RETURN
08620 LET PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_CRITICAL_HIT
08630 RETURN
08640 LET PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_HIT
08650 RETURN
08660 LET PLAYER_ATTACK_EFFECTIVENESS=PLAYER_ATTACK_WEAK_HIT
08670 RETURN
08680 PRINT "MAGIC"
08690 IF PLAYER_EQUIPPED_ITEM<>EQUIPMENT_NONE THEN 08740
08700 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="CLERIC" THEN 08760
08710 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="WIZARD" THEN 09310
08720 PRINT "YOU CANT TSE MAGIC YOUR NOT A M.U."
08730 GOTO 07000
08740 PRINT "YOU CANT USE MAGIC WITH WEAPON IN HAND"
08750 GOTO 07000
08760 PRINT "CLERICAL SPELL #";
08770 INPUT SELECTED_SPELL
08780 FOR LOOP=1 TO PLAYER_CLERIC_SPELL_COUNT
08790 IF SELECTED_SPELL=PLAYER_CLERIC_SPELL_SLOTS(LOOP) THEN 08830
08800 NEXT LOOP
08810 PRINT "YOU DONT HAVE THAT SPELL"
08820 GOTO 07000
08830 LET SELECTED_CLERIC_SPELL=PLAYER_CLERIC_SPELL_SLOTS(LOOP)
08835 LET PLAYER_CLERIC_SPELL_SLOTS(LOOP)=0
08839 IF SELECTED_CLERIC_SPELL=CLERIC_SPELL_KILL THEN 08950
08840 IF SELECTED_CLERIC_SPELL=CLERIC_SPELL_MAGIC_MISSILE_2 THEN 09030
08850 IF SELECTED_CLERIC_SPELL=CLERIC_SPELL_CURE_LIGHT_1 THEN 09060
08860 LET MAP_ITEM_SEARCH=MAP_ITEM_TRAP
08870 IF SELECTED_CLERIC_SPELL=CLERIC_SPELL_FIND_TRAPS THEN 09090
08880 LET MAP_ITEM_SEARCH=MAP_ITEM_SECRET_DOOR
08890 IF SELECTED_CLERIC_SPELL=CLERIC_SPELL_MAGIC_MISSILE_1 THEN 09200
08900 IF SELECTED_CLERIC_SPELL=CLERIC_SPELL_MAGIC_MISSILE_3 THEN 09240
08910 IF SELECTED_CLERIC_SPELL=CLERIC_SPELL_CURE_LIGHT_2 THEN 09280
08920 IF SELECTED_CLERIC_SPELL=CLERIC_SPELL_FIND_SECRET_DOORS THEN 09090
08930 IF SELECTED_CLERIC_SPELL=9 THEN 09720 REM ERROR - SEEMS TO BE A 'TURN UNDEAD' SPELL, BUT NO SUCH SPELL IS DEFINED ELSEWHERE IN THE CODE
08940 GOTO 08810
08950 IF RND(0)*3 > 1 THEN 09000
08960 PRINT "DONE"
08970 LET PLAYER_CLERIC_SPELL_SLOTS(LOOP)=0
08980 LET CURRENT_MONSTER_STATE=MONSTER_STATE_DEAD
08990 GOTO 07000
09000 PRINT "FAILED"
09010 LET PLAYER_CLERIC_SPELL_SLOTS(LOOP)=0
09020 GOTO 07000
09030 PRINT "DONE"
09040 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-4
09050 GOTO 09010
09060 LET PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)=PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)+3 REM ERROR THIS SHOULD PROBABLY BE HITPOINTS, NOT CONSTITUTION
09070 LET PLAYER_CLERIC_SPELL_SLOTS(LOOP)=0
09080 GOTO 07000
09090 LET PLAYER_CLERIC_SPELL_SLOTS(LOOP)=0
09100 FOR LOOP=-3 TO 3
09110 FOR INNER_LOOP=-3 TO 3
09120 IF PLAYER_Y_COORD+LOOP <0 OR PLAYER_Y_COORD+LOOP >25 OR PLAYER_X_COORD+INNER_LOOP<0 OR PLAYER_X_COORD+INNER_LOOP > 25 THEN 09140
09130 IF DUNGEON_MAP(PLAYER_Y_COORD+LOOP,PLAYER_X_COORD+INNER_LOOP)=MAP_ITEM_SEARCH THEN 09180
09140 NEXT INNER_LOOP
09150 NEXT LOOP
09160 PRINT "NO MORE"
09170 GOTO 09010
09180 PRINT "THERE IS ONE AT ";LOOP;"LAT.";INNER_LOOP;"LONG." REM ERROR COORDINATES SHOULD BE REVERSED IF (X,Y) NOTATION IS DESIRED
09190 GOTO 09140
09200 PRINT "DONE"
09210 LET PLAYER_CLERIC_SPELL_SLOTS(LOOP)=0
09220 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-2
09230 GOTO 09010
09240 PRINT "DONE"
09250 LET PLAYER_CLERIC_SPELL_SLOTS(LOOP)=0
09260 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-6
09270 GOTO 09010
09280 PRINT "DONE"
09290 LET PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)=PLAYER_ATTRS(PLAYER_ATTR_CONSTITUTION)+3 REM ERROR THIS SHOULD PROBABLY BE HITPOINTS, NOT CONSTITUTION
09300 GOTO 09010
09310 PRINT "SPELL #";
09320 INPUT SELECTED_SPELL
09330 FOR LOOP=1 TO PLAYER_WIZARD_SPELL_COUNT
09340 IF SELECTED_SPELL=PLAYER_WIZARD_SPELL_SLOTS(LOOP) THEN 09390
09350 NEXT LOOP
09360 PRINT "DO NOT HAVE THAT ONE"
09370 GOTO 01590
09380 IF MONSTER_Y_COORD-PLAYER_Y_COORD=0 THEN 09410
09390 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)<>WIZARD_SPELL_PUSH THEN 09480
09400 GOTO 09420
09410 LET PLAYER_ACTION_Y_COORD_DIR=0
09420 IF MONSTER_X_COORD-PLAYER_X_COORD=0 THEN 09450
09430 PRINT "ARE YOU ABOVE,BELOW,RIGHT, OR LEFT OF IT";REM ERROR THIS TEXT IS OPPOSITE THE LANGUAGE USED BY 'THROW FOOD', EVEN THOUGH BOTH ACTIONS EXECUTE THE SAME LOGIC.  AS SUCH, IT'S PROBABLY BACKWARDS.
09440 GOTO 09470 REM ERROR THIS SHOULD PROBABLY BE 09460
09450 LET PLAYER_ACTION_X_COORD_DIR=0
09460 LET PLAYER_ACTION_MOVE_MONSTER=WIZARD_SPELL_PUSH
09470 GOTO 03940
09480 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_KILL THEN 09660
09490 LET UNUSED_VAR_4=5
09500 LET MAP_ITEM_SEARCH=MAP_ITEM_TRAP
09510 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_FIND_TRAPS THEN 09090
09520 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_TELEPORT THEN 09800
09530 LET SPELL_MAP_ITEM=0
09540 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_CHANGE_1_0 THEN 09860
09550 LET SPELL_DAMAGE=3
09560 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_MAGIC_MISSILE_1 THEN 09950
09570 LET SPELL_DAMAGE=6
09580 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_MAGIC_MISSILE_2 THEN 09950
09590 LET SPELL_DAMAGE=9
09600 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_MAGIC_MISSILE_3 THEN 09950
09610 LET MAP_ITEM_SEARCH=MAP_ITEM_SECRET_DOOR
09620 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_FIND_SECRET_DOORS THEN 09090
09630 LET SPELL_MAP_ITEM=1
09640 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=WIZARD_SPELL_CHANGE_0_1 THEN 09860
09650 GOTO 09360
09660 IF RND(0)*3>1 THEN 09690
09670 PRINT "FAILED"
09680 GOTO 07000
09690 PRINT "DONE"
09700 LET CURRENT_MONSTER_STATE=MONSTER_STATE_DEAD
09710 GOTO 07000
09720 IF CURRENT_MONSTER=MONSTER_SKELETON THEN 09760
09730 IF CURRENT_MONSTER=MONSTER_MUMMY THEN 09760
09740 PRINT "FAILED"
09750 GOTO 07000
09760 PRINT "DONE"
09770 GOTO 09390
09780 LET PLAYER_ACTION_X_COORD_DIR=(MONSTER_X_COORD-PLAYER_X_COORD)/ABS(MONSTER_X_COORD-PLAYER_X_COORD)
09790 GOTO 04220
09800 PRINT "INPUT CO-ORDINATES";
09810 INPUT LOOP,INNER_LOOP
09820 PRINT "DONE"
09830 LET PLAYER_Y_COORD=LOOP
09840 LET PLAYER_X_COORD=INNER_LOOP
09850 GOTO 07000
09860 PRINT "INPUT CO-ORDINATES";
09870 INPUT LOOP,INNER_LOOP
09880 IF DUNGEON_MAP(LOOP,INNER_LOOP)=MAP_ITEM_NONE THEN 09920
09890 IF DUNGEON_MAP(LOOP,INNER_LOOP)=MAP_ITEM_WALL THEN 09920
09900 PRINT "FAILED"
09910 GOTO 07000
09920 LET DUNGEON_MAP(LOOP,INNER_LOOP)=SPELL_MAP_ITEM
09930 PRINT "DONE"
09940 GOTO 07000
09950 PRINT "DONE"
09960 LET MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)=MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)-SPELL_DAMAGE-INT(RND(0)*11)
09965 PRINT "M-HP=";MONSTERS_ATTRS(CURRENT_MONSTER,MONSTER_ATTR_HITPOINTS)
09970 GOTO 07000
09980 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="CLERIC" THEN 10020
09990 IF PLAYER_ATTR_LABELS$(PLAYER_ATTR_LABEL_NAME)="WIZARD" THEN 10360
10000 PRINT "YOU CANT BUY ANY"
10010 GOTO 01590
10020 PRINT "DO YOU KNOW THE CHOICES";
10030 INPUT USER_INPUT$
10040 IF USER_INPUT$="YES" THEN 10100
10050 PRINT "1-KILL-500  5-MAG. MISS. #1-100"
10060 PRINT "2-MAG. MISS. #2-200  6-MAG. MISS. #3-300"
10070 PRINT "3-CURE LHGHT #1-200  7-CURE LIGHT #2-1000"
10080 PRINT "4-FIND ALL TRAPS-200  8-FIND ALL S.DOORS-200"
10090 PRINT "INPUT # WANTED   NEG.NUM.TO STOP";
10100 INPUT SELECTED_SPELL
10110 LET CLERIC_SPELL_COSTS(CLERIC_SPELL_KILL)=500
10120 LET CLERIC_SPELL_COSTS(CLERIC_SPELL_MAGIC_MISSILE_2)=200
10130 LET CLERIC_SPELL_COSTS(CLERIC_SPELL_CURE_LIGHT_1)=200
10140 LET CLERIC_SPELL_COSTS(CLERIC_SPELL_FIND_TRAPS)=200
10150 LET CLERIC_SPELL_COSTS(CLERIC_SPELL_MAGIC_MISSILE_1)=100
10160 LET CLERIC_SPELL_COSTS(CLERIC_SPELL_MAGIC_MISSILE_3)=300
10170 LET CLERIC_SPELL_COSTS(CLERIC_SPELL_CURE_LIGHT_2)=1000
10180 LET CLERIC_SPELL_COSTS(CLERIC_SPELL_FIND_SECRET_DOORS)=200
10190 IF SELECTED_SPELL<CLERIC_SPELL_KILL THEN 10290
10200 IF SELECTED_SPELL>WIZARD_SPELL_CHANGE_0_1 THEN 10100 REM ERROR - SHOULD BE CLERIC_SPELL_FIND_SECRET_DOORS
10210 IF PLAYER_ATTRS(PLAYER_ATTR_GOLD)-CLERIC_SPELL_COSTS(INT(SELECTED_SPELL))<0 THEN 10270
10220 LET PLAYER_ATTRS(PLAYER_ATTR_GOLD)=PLAYER_ATTRS(PLAYER_ATTR_GOLD)-CLERIC_SPELL_COSTS(INT(SELECTED_SPELL))
10230 PRINT "IT IS YOURS"
10240 LET PLAYER_CLERIC_SPELL_COUNT=PLAYER_CLERIC_SPELL_COUNT+1
10250 LET PLAYER_CLERIC_SPELL_SLOTS(PLAYER_CLERIC_SPELL_COUNT)=INT(SELECTED_SPELL)
10260 GOTO 10100
10270 PRINT "COSTS TOO MUCH"
10280 GOTO 10100
10290 PRINT "YOUR SPELLS ARE"
10300 FOR LOOP=1 TO PLAYER_CLERIC_SPELL_COUNT
10310 IF PLAYER_CLERIC_SPELL_SLOTS(LOOP)=0 THEN 10330
10320 PRINT "#";PLAYER_CLERIC_SPELL_SLOTS(LOOP)
10330 NEXT LOOP
10340 PRINT "DONE"
10350 GOTO 01590
10360 PRINT "DO YOU KNOW THE SPELLS";
10370 INPUT USER_INPUT$
10380 IF USER_INPUT$="YES" THEN 10450
10390 PRINT "1-PUSH-75   6-M. M. #1-100"
10400 PRINT "2-KIHL-500  7-M. M. #2-200"
10410 PRINT "3-FIND TRAPS-200  8-M. M. #3-300"
10420 PRINT "4-TELEPORT-750  9-FIND S.DOORS-200"
10430 PRINT "5-CHANGE 1+0-600  10-CHANGE 0+1-600"
10440 PRINT "#OF ONE YOU WANT  NEG.NUM.TO STOP";
10450 INPUT SELECTED_SPELL
10460 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_PUSH)=75
10470 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_KILL)=500
10480 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_FIND_TRAPS)=200
10490 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_TELEPORT)=750
10500 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_CHANGE_1_0)=600
10510 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_MAGIC_MISSILE_1)=100
10520 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_MAGIC_MISSILE_2)=200
10530 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_MAGIC_MISSILE_3)=300
10540 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_FIND_SECRET_DOORS)=200
10550 LET WIZARD_SPELL_COSTS(WIZARD_SPELL_CHANGE_0_1)=600
10560 IF SELECTED_SPELL<WIZARD_SPELL_PUSH THEN 10660
10570 IF SELECTED_SPELL>CLERIC_SPELL_FIND_SECRET_DOORS THEN 10450 REM ERROR - SHOULD BE WIZARD_SPELL_CHANGE_0_1
10580 IF PLAYER_ATTRS(PLAYER_ATTR_GOLD)-WIZARD_SPELL_COSTS(INT(SELECTED_SPELL))<0 THEN 10640
10590 LET PLAYER_ATTRS(PLAYER_ATTR_GOLD)=PLAYER_ATTRS(PLAYER_ATTR_GOLD)-WIZARD_SPELL_COSTS(INT(SELECTED_SPELL))
10600 PRINT "IT IS YOURS"
10610 LET PLAYER_WIZARD_SPELL_COUNT=PLAYER_WIZARD_SPELL_COUNT+1
10620 LET PLAYER_WIZARD_SPELL_SLOTS(PLAYER_WIZARD_SPELL_COUNT)=INT(SELECTED_SPELL)
10630 GOTO 10450
10640 PRINT "COSTS TOO MUCH"
10650 GOTO 10450
10660 PRINT "YOU NOW HAVE"
10670 FOR LOOP=1 TO PLAYER_WIZARD_SPELL_COUNT
10680 IF PLAYER_WIZARD_SPELL_SLOTS(LOOP)=0 THEN 00700
10690 PRINT "#";PLAYER_WIZARD_SPELL_SLOTS(LOOP)
10700 NEXT LOOP
10710 GOTO 01590

10720 REM
10730 REM CHEATING
10740 FOR LOOP=0 TO 25
10750 FOR INNER_LOOP=0 TO 25
10760 PRINT DUNGEON_MAP(LOOP,INNER_LOOP);
10770 NEXT INNER_LOOP
10780 PRINT
10790 NEXT LOOP
10800 GOTO 01590

10810 REM
10820 GOTO 00380
10830 PRINT "HOW MANY 200 GP. EACH ";
10840 INPUT USER_INPUT_PURCHASED_HITPOINTS
10850 IF PLAYER_ATTRS(PLAYER_ATTR_GOLD)-HITPOINT_COST*USER_INPUT_PURCHASED_HITPOINTS<0 THEN 10900
10860 LET PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)=PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)+INT(USER_INPUT_PURCHASED_HITPOINTS)
10870 LET PLAYER_ATTRS(PLAYER_ATTR_GOLD)=PLAYER_ATTRS(PLAYER_ATTR_GOLD)-INT(USER_INPUT_PURCHASED_HITPOINTS*HITPOINT_COST)
10880 PRINT "OK DONE"
10885 PRINT "HP= ";PLAYER_ATTRS(PLAYER_ATTR_HITPOINTS)
10886 FOR LOOP=1 TO PLAYER_ATTRS_COUNT
10887 PRINT PLAYER_ATTR_LABELS$(LOOP);" = ";PLAYER_ATTRS(LOOP)
10888 NEXT LOOP
10890 GOTO 07000
10900 PRINT "NO"
10910 GOTO 10830
11000 PRINT "DNG";
11010 INPUT MAP_EDIT_DUNGEON_NUM
11020 PRINT "X,Y,C";
11030 INPUT MAP_EDIT_X_COORD,MAP_EDIT_Y_COORD,MAP_EDIT_ITEM
11035 IF MAP_EDIT_ITEM<0 THEN 11060
11040 LET DUNGEON_MAP(MAP_EDIT_X_COORD,MAP_EDIT_Y_COORD)=MAP_EDIT_ITEM REM ERROR SHOULD BE DUMGEON_MAP(MAP_EDIT_Y_COORD,MAP_EDIT_X_COORD)=MAP_EDIT_ITEM
11050 GOTO 11020
11060 PRINT "SAVE"
11061 INPUT USER_INPUT_SAVE
11062 IF USER_INPUT_SAVE<>1 THEN 07000
11063 FOR LOOP=0 TO 25
11070 FOR INNER_LOOP=0 TO 25
11080 WRITE #MAP_EDIT_DUNGEON_NUM,DUNGEON_MAP(LOOP,INNER_LOOP)
11090 NEXT INNER_LOOP
11100 NEXT LOOP
11110 GOTO 07000
11120 REM END

99000 REM DEFINE ALL CONSTANTS TO ELIMINATE 'MAGIC NUMBERS'
99001 LET CLERIC_SPELL_KILL=1
99002 LET CLERIC_SPELL_MAGIC_MISSILE_2=2
99003 LET CLERIC_SPELL_CURE_LIGHT_1=3
99004 LET CLERIC_SPELL_FIND_TRAPS=4
99005 LET CLERIC_SPELL_MAGIC_MISSILE_1=5
99006 LET CLERIC_SPELL_MAGIC_MISSILE_3=6
99007 LET CLERIC_SPELL_CURE_LIGHT_2=7
99008 LET CLERIC_SPELL_FIND_SECRET_DOORS=8
99009 LET COMMAND_PASS=0
99010 LET COMMAND_MOVE=1
99011 LET COMMAND_OPEN_DOOR=2
99012 LET COMMAND_SEARCH=3
99013 LET COMMAND_SWITCH_WEAPON_HAND=4
99014 LET COMMAND_FIGHT=5
99015 LET COMMAND_LOOK_AROUND=6
99016 LET COMMAND_SAVE=7
99017 LET COMMAND_USE_MAGIC_SPELL=8
99018 LET COMMAND_BUY_MAGIC_SPELL=9
99019 LET COMMAND_VIEW_MAP=10
99020 LET COMMAND_BUY_HITPOINTS=11
99021 LET COMMAND_EDIT_MAP=12
99022 LET DICE_CRITICAL_HIT_THRESHOLD=0.97
99023 LET DICE_SIX_SIDED=6
99024 LET EQUIPMENT_NONE=0
99025 LET EQUIPMENT_SWORD=1
99026 LET EQUIPMENT_TWO_HANDED_SWORD=2
99027 LET EQUIPMENT_DAGGER=3
99028 LET EQUIPMENT_MACE=4
99029 LET EQUIPMENT_SPEAR=5
99030 LET EQUIPMENT_BOW=6
99031 LET EQUIPMENT_ARROWS=7
99032 LET EQUIPMENT_LEATHER_MAIL=8
99033 LET EQUIPMENT_CHAIN_MAIL=9
99034 LET EQUIPMENT_PLATE_MAIL=10
99035 LET EQUIPMENT_ROPE=11
99036 LET EQUIPMENT_SPIKES=12
99037 LET EQUIPMENT_FLASK_OF_OIL=13
99038 LET EQUIPMENT_SILVER_CROSS=14
99039 LET EQUIPMENT_SPARE_FOOD=15
99040 LET EQUIPMENT_FIRST=EQUIPMENT_SWORD
99041 LET EQUIPMENT_LAST=EQUIPMENT_SPARE_FOOD
99042 LET HITPOINT_COST=200
99043 LET MAP_ITEM_NONE=0
99044 LET MAP_ITEM_WALL=1
99045 LET MAP_ITEM_TRAP=2
99046 LET MAP_ITEM_SECRET_DOOR=3
99047 LET MAP_ITEM_SECRET_DOOR_TOO=4
99048 LET MAP_ITEM_MONSTER=5
99049 LET MAP_ITEM_GOLD_TRAP=6
99050 LET MAP_ITEM_STRENGTH_TRAP=7
99051 LET MAP_ITEM_CONSTITUTION_TRAP=8
99052 LET MAP_ITEM_FIRST=MAP_ITEM_NONE
99053 LET MAP_ITEM_LAST=MAP_ITEM_CONSTITUTION_BOOST
99054 LET MONSTER_ATTR_LEVEL=1
99055 LET MONSTER_ATTR_COMBAT_SKILL=2
99056 LET MONSTER_ATTR_HITPOINTS=3
99057 LET MONSTER_ATTR_INITIAL_HITPOINTS=4
99058 LET MONSTER_ATTR_INITIAL_GOLD=5
99059 LET MONSTER_ATTR_GOLD=6
99060 LET MONSTER_ATTRS_COUNT=MONSTER_ATTR_GOLD
99061 LET MONSTER_NONE=0
99062 LET MONSTER_MAN=1
99063 LET MONSTER_GOBLIN=2
99064 LET MONSTER_TROLL=3
99065 LET MONSTER_SKELETON=4
99066 LET MONSTER_BALROG=5
99067 LET MONSTER_OCHRE_JELLY=6
99068 LET MONSTER_GREY_OOZE=7
99069 LET MONSTER_GNOME=8
99070 LET MONSTER_KOBOLD=9
99071 LET MONSTER_MUMMY=10
99072 LET MONSTERS_COUNT=MONSTER_LAST
99073 LET MONSTER_FIRST=MONSTER_MAN
99074 LET MONSTER_LAST=MONSTER_MUMMY
99075 LET MONSTER_STATE_ALIVE=0
99076 LET MONSTER_STATE_DEAD=-1
99077 LET PLAYER_ACTION_LURE_MONSTER=0
99078 LET PLAYER_ATTR_HITPOINTS=0
99079 LET PLAYER_ATTR_STRENGTH=1
99080 LET PLAYER_ATTR_DEXTERITY=2
99081 LET PLAYER_ATTR_CONSTITUTION=3
99082 LET PLAYER_ATTR_CHARISMA=4
99083 LET PLAYER_ATTR_WISDOM=5
99084 LET PLAYER_ATTR_INTELLIGENCE=6
99085 LET PLAYER_ATTR_GOLD=7
99086 LET PLAYER_ATTR_LABEL_NAME=0
99087 LET PLAYER_ATTRS_COUNT=PLAYER_ATTR_GOLD
99088 LET PLAYER_ATTRS_DICE_ROLLS=3
99089 LET PLAYER_ATTACK_MISS=0
99090 LET PLAYER_ATTACK_WEAK_HIT=1
99091 LET PLAYER_ATTACK_HIT=2
99092 LET PLAYER_ATTACK_CRITICAL_HIT=3
99093 LET WIZARD_SPELL_PUSH=1
99094 LET WIZARD_SPELL_KILL=2
99095 LET WIZARD_SPELL_FIND_TRAPS=3
99096 LET WIZARD_SPELL_TELEPORT=4
99097 LET WIZARD_SPELL_CHANGE_1_0=5
99098 LET WIZARD_SPELL_MAGIC_MISSILE_1=6
99099 LET WIZARD_SPELL_MAGIC_MISSILE_2=7
99100 LET WIZARD_SPELL_MAGIC_MISSILE_3=8
99101 LET WIZARD_SPELL_FIND_SECRET_DOORS=9
99102 LET WIZARD_SPELL_CHANGE_0_1=10
99998 RETURN
99999 END

REM --------------------------------------------------------------------------
REM VERSION HISTORY:
REM --------------------------------------------------------------------------
REM Version 1.5: 2014-04-30 (DND1-Remastered.pdf)
REM Version 1.5: 2014-04-30 (DND1_Complete.bas)
REM Version 1.5: 2014-04-30 (DND1-Restructured.bas)
REM  - Updated version information to be consistent across documents.
REM
REM Version 1.5: 2014-04-30 (DND-Named.bas)
REM  - Released a version that renames all basic variables and "magic numbers"
REM      with verbose counterparts, for clarity.
REM
REM Version 1.5: 2014-04-30 (DND-Named-Restructured.bas)
REM  - Released a version that reorders and indents source code lines, and
REM      uses verbose variable names and constants to truly clarify the code.
REM
REM Version 1.4: 2014-04-24 (DND1-Restructured.bas)
REM  - Released a version that reorders and indents source code lines so that
REM      the programming logic looks more structured, and is easier to
REM      analyze.
REM
REM Version 1.3: 2014-04-20 (DND1-Remastered.pdf)
REM  - Fixed transcription errors in lines 00300, 00430, 01090, 01240, 01920,
REM      02940, 03920, 04750, 05980, 07290, 07300, 07931, 08290, 08470, 08720,
REM      09080
REM
REM Version 1.2: 2014-04-19 (DND1-Remastered.pdf)
REM  - Fixed transcription error in line 06450
REM
REM Version 1.1: 2014-04-17 (DND1-Remastered.pdf)
REM  - Added notices and version history
REM
REM Version 1.0: 2014-04-17 (DND1-Remastered.pdf)
REM  - Initial version, transcribed in 2014 by Dejay Clayton, from a scan of
REM      the original PDP-11 teletype program listing made availble by Richard
REM      Garriott.
REM --------------------------------------------------------------------------
